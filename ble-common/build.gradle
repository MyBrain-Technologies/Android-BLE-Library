apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 30

        versionCode 101
        versionName '2.3.1.mbt.rc2'
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'ble-common-proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def useLocalModuleBle = false
    if (useLocalModuleBle) {
        api project(':ble')
    } else {
        api 'no.nordicsemi.android:ble:2.3.1.mbt.rc1'
    }

    testImplementation 'junit:junit:4.13.2'
}

//************************************************
//* publish artifact to maven
//* add your credentials in local.properties
//* to run from terminal: ./gradlew :ble-common:publishReleasePublicationToMavenRepository --stacktrace
//************************************************
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def mavenUsername = properties.getProperty('maven_user')
def mavenPassword = properties.getProperty('maven_password')
def releasesRepoUrl = 'https://package.mybraintech.com/repository/maven-2-releases/'
def snapshotsRepoUrl = 'https://package.mybraintech.com/repository/maven-2-snapshots/'

project.afterEvaluate {
    publishing {
        repositories {
            maven {
                url = android.defaultConfig.versionName.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username mavenUsername
                    password mavenPassword
                }
            }
        }
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release

                groupId = 'no.nordicsemi.android'
                artifactId = 'ble-common'
                version = android.defaultConfig.versionName
            }
        }
    }
}